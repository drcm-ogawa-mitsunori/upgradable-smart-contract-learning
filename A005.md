# A005 ERC-1967 を見てみよう

ここまで見てきた storage slot の特性を踏まえて、 OpenZeppelin Upgradable Contracts 等で採用されている Proxy contract の標準仕様となった ERC-1967 について、中身を深堀りしてみましょう。
https://eips.ethereum.org/EIPS/eip-1967
Implementation contract address など Proxy が定常的に持つべき値について、 Implementation 側からその storage slot に値を入れられたりしないように、特定の storage slot を使いましょうという仕様です。
標準的な Solidity コンパイラが指定し得ない場所を使っています。
例えば OpenZeppelin Proxy が持つ Logic(Implementation) address の slot number は `0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc` となっており、これは `bytes32(uint256(keccak256('eip1967.proxy.implementation')) - 1)` と同値です。

```
library StorageSlot {
    struct AddressSlot {
        address value;
    }

    struct BooleanSlot {
        bool value;
    }

    struct Bytes32Slot {
        bytes32 value;
    }

    struct Uint256Slot {
        uint256 value;
    }

    /**
     * @dev Returns an `AddressSlot` with member `value` located at `slot`.
     */
    function getAddressSlot(bytes32 slot) internal pure returns (AddressSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `BooleanSlot` with member `value` located at `slot`.
     */
    function getBooleanSlot(bytes32 slot) internal pure returns (BooleanSlot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Bytes32Slot` with member `value` located at `slot`.
     */
    function getBytes32Slot(bytes32 slot) internal pure returns (Bytes32Slot storage r) {
        assembly {
            r.slot := slot
        }
    }

    /**
     * @dev Returns an `Uint256Slot` with member `value` located at `slot`.
     */
    function getUint256Slot(bytes32 slot) internal pure returns (Uint256Slot storage r) {
        assembly {
            r.slot := slot
        }
    }
}
```

このような library を定義し、以下のように利用しています。

```
/**
 * @dev This abstract contract provides getters and event emitting update functions for
 * https://eips.ethereum.org/EIPS/eip-1967[EIP1967] slots.
 */
abstract contract ERC1967Upgrade {
    // This is the keccak-256 hash of "eip1967.proxy.rollback" subtracted by 1
    bytes32 private constant _ROLLBACK_SLOT = 0x4910fdfa16fed3260ed0e7147f7cc6da11a60208b5b9406d12a635614ffd9143;

    /**
     * @dev Storage slot with the address of the current implementation.
     * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1, and is
     * validated in the constructor.
     */
    bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

    /**
     * @dev Emitted when the implementation is upgraded.
     */
    event Upgraded(address indexed implementation);

    /**
     * @dev Returns the current implementation address.
     */
    function _getImplementation() internal view returns (address) {
        return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;
    }
```

Upgradable Contracts 以外に delegatecall を利用した contract の仕組みを作ろう！と考えている場合は、同じように storage slot について考慮しておくのが良いと考えます。
